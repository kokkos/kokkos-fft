# SPDX-FileCopyrightText: (C) The kokkos-fft development team, see COPYRIGHT.md file
#
# SPDX-License-Identifier: MIT OR Apache-2.0 WITH LLVM-exception

@PACKAGE_INIT@

# Set and Check must go first else macro might be redefined by find_dependency ...
set_and_check(KokkosFFT_TARGET_FILE "${PACKAGE_PREFIX_DIR}/@LIBDIR@/@KokkosFFT_EXPORT_TARGET@.cmake")

include(CMakeFindDependencyMacro)
list(INSERT CMAKE_MODULE_PATH 0 "${PACKAGE_PREFIX_DIR}/@LIBDIR@")
find_dependency(Kokkos)

if(@KokkosFFT_ENABLE_CUFFT@ AND @Kokkos_ENABLE_CUDA@)
  find_dependency(CUDAToolkit COMPONENTS cufft)
endif()

if(@KokkosFFT_ENABLE_ROCFFT@ AND @Kokkos_ENABLE_HIP@)
  find_dependency(rocfft)
endif()

if(@KokkosFFT_ENABLE_HIPFFT@ AND @Kokkos_ENABLE_HIP@)
  find_dependency(hipfft)
endif()

if(@KokkosFFT_ENABLE_ONEMKL@ AND @Kokkos_ENABLE_SYCL@)
  find_dependency(MKL COMPONENTS SYCL)
endif()

if(@KokkosFFT_ENABLE_FFTW@)
  find_dependency(FFTW MODULE)
endif()

include(${KokkosFFT_TARGET_FILE})

check_required_components(KokkosFFT)
